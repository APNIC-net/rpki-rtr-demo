#!/usr/bin/perl

use warnings;
use strict;

use APNIC::RPKI::RTR::Server;

sub usage
{
    print <<EOF;
rpki-rtr-server {data-directory}
    init
    --server {server}
    --port {port}
    [--strict]
    [--tcp-md5-key {key}]
    [--cert-file {path} --key-file {path}]
    [--ca-file {path}]

  Starts a new server, using the specified data directory.  If
  "--ca-file" is specified, all clients must connect using client
  certificates.
EOF
}

sub error
{
    my ($error) = @_;

    print STDERR "$error\n";

    usage();

    exit(10);
}

sub main
{
    my ($dir, $op_name, @args) = @_;

    if (not $dir) {
        error("Data directory must be provided.");
    }
    if (not -d $dir) {
        my $res = mkdir $dir;
        if (not $res) {
            error("Unable to make directory: $!");
        }
    }

    if (not $op_name) {
        error("Operation name not provided.");
    } elsif ($op_name eq 'init') {
        my $server;
        my $port;
        my $tcp_md5_key;
        my $cert_file;
        my $key_file;
        my $ca_file;
        my $strict;
        while (@args) {
            my $key = shift @args;
            if ($key eq '--server') {
                $server = shift @args;
            } elsif ($key eq '--port') {
                $port = shift @args;
            } elsif ($key eq '--strict') {
                $strict = 1;
            } elsif ($key eq '--tcp-md5-key') {
                $tcp_md5_key = shift @args;
            } elsif ($key eq '--cert-file') {
                $cert_file = shift @args;
            } elsif ($key eq '--key-file') {
                $key_file = shift @args;
            } elsif ($key eq '--ca-file') {
                $ca_file = shift @args;
            } else {
                error("Invalid argument '$key'");
            }
        }
        if (not $server or not $port) {
            error("Server and port must be provided.");
        }

        my $server_obj =
            APNIC::RPKI::RTR::Server->new(
                data_dir    => $dir,
                server      => $server,
                port        => $port,
                tcp_md5_key => $tcp_md5_key,
                cert_file   => $cert_file,
                key_file    => $key_file,
                ca_file     => $ca_file,
                (($strict)
                    ? (strict_send    => 1,
                       strict_receive => 1)
                    : ())
            );

        $server_obj->run();

        return 1;
    } else {
        error("Unknown operation name '$op_name'.");
    }
}

main(@ARGV);

1;
